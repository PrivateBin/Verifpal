attacker[passive]

principal Server[]

principal Reader[]

principal Creator[
	knows private paste
	knows password optional_secret
	generates key, cipher_iv, kdf_salt
	// PBKDF2
	kdf_key1 = PW_HASH(CONCAT(key, optional_secret))
	cipher_text = AEAD_ENC(kdf_key1, paste, CONCAT(cipher_iv, kdf_salt))
]

Creator -> Server: cipher_text, cipher_iv, kdf_salt

principal Server [
	generates paste_id
]

Server -> Creator: paste_id

phase[1]

Creator -> Reader: paste_id, key, optional_secret

phase[2]

// in response to: Reader -> Server: paste_id
Server -> Reader: cipher_text, cipher_iv, kdf_salt

principal Reader[
	kdf_key2 = PW_HASH(CONCAT(key, optional_secret))
	dec_paste = AEAD_DEC(kdf_key2, cipher_text, CONCAT(cipher_iv, kdf_salt))?
]

queries[
	// this is the one and only thing that is protected:
	confidentiality? paste
	// both key and optional_password can be optained even by a passive attacker:
	// confidentiality? key
	// confidentiality? optional_secret
	// the server or the attacker can spoof the message, so this fails:
	// authentication? Creator -> Reader: cipher_text
]
